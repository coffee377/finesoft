pluginManagement {
    repositories {
        /* 本地参考地址 */
        maven { url "D:/SoftWare/Maven/repository" }
        /* 公司仓库地址 */
        maven { url "http://192.168.44.155/nexus/content/groups/public/" }
        /* 插件门户地址 */
        gradlePluginPortal()
    }
}

static String sanitizeName(String name) {
    return name.replaceAll("[ /\\\\:<>\"?*|]", "_")
}

static def isTaskDir(File dir) {
    return new File(dir, "build.gradle").exists()
}

rootProject.projectDir.eachDirRecurse {
    if (!isTaskDir(it) || it.path.contains(".idea")) {
        return
    }
    def taskRelativePath = rootDir.toPath().relativize(it.toPath())
    def parts = []
    for (name in taskRelativePath) {
        parts.add(sanitizeName(name.toString()))
    }
    def moduleName = parts.get(parts.size() - 1)
//    def moduleName = parts.join("-")
    include "$moduleName"
    project(":$moduleName").projectDir = it
}


rootProject.name = 'finesoft'

include(":plugin-annotation-processor")
